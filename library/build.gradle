apply plugin: 'com.android.library'
apply plugin: 'jacoco'
apply plugin: 'maven'

jacoco {
    toolVersion = "0.7.9"
}

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0.6"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            all{
                jacoco {
                    includeNoLocationClasses = true
                }
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    provided 'com.android.support:appcompat-v7:25.4.0'
    provided 'com.android.support:design:25.4.0'
    provided 'com.android.support:recyclerview-v7:25.4.0'
    provided 'com.google.code.gson:gson:2.8.2'
    testCompile 'junit:junit:4.12'
}


project.afterEvaluate {
    android.libraryVariants.all { variant ->
        def name = variant.name
        def testTaskName = "test${name.capitalize()}UnitTest"
        tasks.create(name: "${testTaskName}Coverage", type: JacocoReport, dependsOn: "$testTaskName") {
            group = "Reporting"
            description = "Generate Jacoco coverage reports for the ${name.capitalize()} build."
            classDirectories = fileTree(
                    dir: "${project.buildDir}/intermediates/classes/${name}",
                    includes: ['**/com/silverhetch/util/**'],
                    excludes: ['**/R.class',
                               '**/R$*.class',
                               '**/*$ViewInjector*.*',
                               '**/*$ViewBinder*.*',
                               '**/BuildConfig.*',
                               '**/Manifest*.*']
            )
            sourceDirectories = files(['src/main/java'].plus(android.sourceSets[name].java.srcDirs))
            executionData = files("${project.buildDir}/jacoco/${testTaskName}.exec")
            reports {
                xml.enabled = true
                html.enabled = true
            }
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            snapshotRepository(url: "http://172.104.79.181:81/repository/maven-snapshots/") {
                authentication(userName: "larryhsiao", password: "snake")
                pom.groupId = "com.silverhetch"
                pom.artifactId = "aura"
                pom.version = android.defaultConfig.versionName+ '-SNAPSHOT'
            }
        }
    }
}