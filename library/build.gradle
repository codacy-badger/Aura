apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'jacoco'
apply plugin: 'maven'

def deployFile = file("deploy/deploy.properties")
def deployProperties = new Properties()
deployProperties.load(new FileInputStream(deployFile))

jacoco {
    toolVersion = "0.7.9"
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27
        versionCode Integer.valueOf(deployProperties['versionCode'])
        versionName deployProperties['versionName']

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            all{
                jacoco {
                    includeNoLocationClasses = true
                }
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    provided 'com.android.support:support-fragment:27.1.0'
    provided 'com.android.support:appcompat-v7:27.1.0'
    provided 'com.android.support:design:27.1.0'
    provided 'com.android.support:recyclerview-v7:27.1.0'
    provided 'com.google.code.gson:gson:2.8.2'
    testCompile 'junit:junit:4.12'
    provided "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}


project.afterEvaluate {
    android.libraryVariants.all { variant ->
        def name = variant.name
        def testTaskName = "test${name.capitalize()}UnitTest"
        tasks.create(name: "${testTaskName}Coverage", type: JacocoReport, dependsOn: "$testTaskName") {
            group = "Reporting"
            description = "Generate Jacoco coverage reports for the ${name.capitalize()} build."
            classDirectories = fileTree(
                    dir: "${project.buildDir}/intermediates/classes/${name}",
                    includes: ['**/com/silverhetch/util/**'],
                    excludes: ['**/R.class',
                               '**/R$*.class',
                               '**/*$ViewInjector*.*',
                               '**/*$ViewBinder*.*',
                               '**/BuildConfig.*',
                               '**/Manifest*.*']
            )
            sourceDirectories = files(['src/main/java'].plus(android.sourceSets[name].java.srcDirs))
            executionData = files("${project.buildDir}/jacoco/${testTaskName}.exec")
            reports {
                xml.enabled = true
                html.enabled = true
            }
        }
    }
}


uploadArchives {
    repositories {
        mavenDeployer {
            snapshotRepository(url:deployProperties['repository'] ) {
                authentication(userName: deployProperties['username'], password: deployProperties['password'])
                pom.groupId = deployProperties['groupId']
                pom.artifactId = deployProperties['artifactId']
                pom.version = deployProperties['versionName']+ '-SNAPSHOT'
            }
        }
    }
}
repositories {
    mavenCentral()
}